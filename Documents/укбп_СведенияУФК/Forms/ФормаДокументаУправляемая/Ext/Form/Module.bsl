
&НаСервере
Процедура ЗаполнитьНаСервере()
	
	#Область Инициализация_переменных
	НачалоПериода 	= НачалоГода(Объект.Дата);
	КонецПериода	= КонецГода(Объект.Дата);
	
	ТипЦенОриент = Справочники.ТипыЦенНоменклатуры.НайтиПоКоду("000000021"); //Ориентировочная цена
	ТипЦенФиксир = Справочники.ТипыЦенНоменклатуры.НайтиПоКоду("000000022"); //Фиксированная цена
	
	СтатьяУФК7440 = Справочники.укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.НайтиПоКоду("7440"); //7440 Средства по контрактам (договорам), заключаемым в целях исполнения государственных контрактов
	СтатьяПрибыль = Справочники.укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.НайтиПоКоду("0999"); //0999 Выплата прибыли
	СтатьяНДС = Справочники.укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.НайтиПоКоду("0811"); //0811 Налог на добавленную стоимость 
	#КонецОбласти
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.УстановитьПараметр("ТипЦенОриент", ТипЦенОриент);
	Запрос.УстановитьПараметр("ТипЦенФиксир", ТипЦенФиксир);
	
	Запрос.УстановитьПараметр("СтатьяУФК7440", СтатьяУФК7440);
	Запрос.УстановитьПараметр("СтатьяПрибыль", СтатьяПрибыль);
	Запрос.УстановитьПараметр("СтатьяНДС", СтатьяНДС);
	
	Запрос.УстановитьПараметр("БанковскийСчетОрганизации", Объект.БанковскийСчет);	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	укбп_ЗаказЗаявка.Ссылка КАК Ссылка,
	|	укбп_БанковскиеСчетаДоговоров.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	РегистрСведений.укбп_БанковскиеСчетаДоговоров КАК укбп_БанковскиеСчетаДоговоров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.укбп_ЗаказЗаявка КАК укбп_ЗаказЗаявка
	|		ПО укбп_БанковскиеСчетаДоговоров.Контрагент = укбп_ЗаказЗаявка.Контрагент
	|			И укбп_БанковскиеСчетаДоговоров.ДоговорКонтрагента = укбп_ЗаказЗаявка.ДоговорКонтрагента
	|ГДЕ
	|	укбп_БанковскиеСчетаДоговоров.БанковскийСчетОрганизации.ТипБанковскогоСчета = ЗНАЧЕНИЕ(Перечисление.укбп_ТипыБанковскихСчетов.КазначейскоеСопровождение)
	|	И укбп_БанковскиеСчетаДоговоров.БанковскийСчетОрганизации = &БанковскийСчетОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДенежныеСредстваОстаткиИОбороты.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаПриход КАК СуммаПриход
	|ПОМЕСТИТЬ ТаблицаСДенежнымиСредствами
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			,
	|			БанковскийСчетКасса В
	|				(ВЫБРАТЬ
	|					ТаблицаДокументов.БанковскийСчетОрганизации
	|				ИЗ
	|					ТаблицаДокументов КАК ТаблицаДокументов)) КАК ДенежныеСредстваОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидОперации КАК ВидОперации,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидДоговора КАК ВидДоговора,
	|	укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	укбп_ЗаказЗаявкаИзделия.Количество * укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.Сумма КАК СуммаОриентировочная,
	|	0 КАК СуммаФиксированная,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.укбп_ВидРаздельногоУчета КАК укбп_ВидРаздельногоУчета
	|ПОМЕСТИТЬ Объединение
	|ИЗ
	|	Документ.укбп_ЗаказЗаявка.Изделия КАК укбп_ЗаказЗаявкаИзделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.укбп_КалькуляцияНаИзделие.КалькуляцияЗатрат КАК укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат
	|		ПО укбп_ЗаказЗаявкаИзделия.КалькуляцияНаИзделие = укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.Ссылка
	|ГДЕ
	|	укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.Ссылка.ТипЦен = &ТипЦенОриент
	|	И укбп_ЗаказЗаявкаИзделия.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаДокументов.Ссылка
	|			ИЗ
	|				ТаблицаДокументов КАК ТаблицаДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидОперации,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидДоговора,
	|	укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.СтатьяКалькуляции,
	|	0,
	|	укбп_ЗаказЗаявкаИзделия.Количество * укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.Сумма,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.укбп_ВидРаздельногоУчета
	|ИЗ
	|	Документ.укбп_ЗаказЗаявка.Изделия КАК укбп_ЗаказЗаявкаИзделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.укбп_КалькуляцияНаИзделие.КалькуляцияЗатрат КАК укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат
	|		ПО укбп_ЗаказЗаявкаИзделия.КалькуляцияНаИзделие = укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.Ссылка
	|ГДЕ
	|	укбп_КалькуляцияНаИзделиеКалькуляцияЗатрат.Ссылка.ТипЦен = &ТипЦенФиксир
	|	И укбп_ЗаказЗаявкаИзделия.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаДокументов.Ссылка
	|			ИЗ
	|				ТаблицаДокументов КАК ТаблицаДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидОперации,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидДоговора,
	|	""Прибыль"",
	|	0,
	|	укбп_ЗаказЗаявкаИзделия.КалькуляцияНаИзделие.Прибыль * укбп_ЗаказЗаявкаИзделия.Количество,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.укбп_ВидРаздельногоУчета
	|ИЗ
	|	Документ.укбп_ЗаказЗаявка.Изделия КАК укбп_ЗаказЗаявкаИзделия
	|ГДЕ
	|	укбп_ЗаказЗаявкаИзделия.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаДокументов.Ссылка
	|			ИЗ
	|				ТаблицаДокументов КАК ТаблицаДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидОперации,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.ВидДоговора,
	|	""НДС"",
	|	0,
	|	ВЫБОР
	|		КОГДА укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|				ИЛИ укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|			ТОГДА 0.18
	|		КОГДА укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ИЛИ укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|			ТОГДА 0.2
	|		КОГДА укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|				ИЛИ укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|			ТОГДА 0.1
	|		КОГДА укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ИЛИ укбп_ЗаказЗаявкаИзделия.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 0
	|	КОНЕЦ * укбп_ЗаказЗаявкаИзделия.КалькуляцияНаИзделие.Цена * укбп_ЗаказЗаявкаИзделия.Количество,
	|	укбп_ЗаказЗаявкаИзделия.Ссылка.укбп_ВидРаздельногоУчета
	|ИЗ
	|	Документ.укбп_ЗаказЗаявка.Изделия КАК укбп_ЗаказЗаявкаИзделия
	|ГДЕ
	|	укбп_ЗаказЗаявкаИзделия.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаДокументов.Ссылка
	|			ИЗ
	|				ТаблицаДокументов КАК ТаблицаДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Объединение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.укбп_ВидОперацииЗаказЗаявка.ПоставкаУслуги)
	|			ТОГДА ВЫБОР
	|					КОГДА Объединение.ВидДоговора.Госконтракт
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.укбп_ВидыРаздельногоУчета.Гоз)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.укбп_ВидыРаздельногоУчета.Минпромторг)
	|				КОНЕЦ
	|		ИНАЧЕ Объединение.укбп_ВидРаздельногоУчета
	|	КОНЕЦ КАК ВидРаздельногоУчета,
	|	Объединение.ВидОперации КАК ВидОперации,
	|	Объединение.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	СУММА(Объединение.СуммаФиксированная + Объединение.СуммаОриентировочная) КАК ВсегоПоКалькуляциям,
	|	СУММА(Объединение.СуммаФиксированная) КАК СуммаФиксированная,
	|	СУММА(Объединение.СуммаОриентировочная) КАК СуммаОриентировочная
	|ПОМЕСТИТЬ ОбъединениеСгруп
	|ИЗ
	|	Объединение КАК Объединение
	|ГДЕ
	|	Объединение.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.укбп_ВидОперацииЗаказЗаявка.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Объединение.СтатьяКалькуляции,
	|	Объединение.ВидОперации,
	|	ВЫБОР
	|		КОГДА Объединение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.укбп_ВидОперацииЗаказЗаявка.ПоставкаУслуги)
	|			ТОГДА ВЫБОР
	|					КОГДА Объединение.ВидДоговора.Госконтракт
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.укбп_ВидыРаздельногоУчета.Гоз)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.укбп_ВидыРаздельногоУчета.Минпромторг)
	|				КОНЕЦ
	|		ИНАЧЕ Объединение.укбп_ВидРаздельногоУчета
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДССтатьиКалькуляцииЗатрат.Ссылка.УкрупненныйКодСведений КАК УкрупненныйКод,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДССтатьиКалькуляцииЗатрат.Ссылка.Код КАК ДетализированныйКод,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДССтатьиКалькуляцииЗатрат.Ссылка КАК ДетализированнаяСтатьяУФК,
	|	ОбъединениеСгруп.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ОбъединениеСгруп.ВидРаздельногоУчета КАК ВидРаздельногоУчета,
	|	ОбъединениеСгруп.ВидОперации КАК ВидОперации,
	|	ОбъединениеСгруп.ВсегоПоКалькуляциям КАК ВсегоПоКалькуляциям,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДССтатьиКалькуляцииЗатрат.Ссылка.НДС КАК СтавкаНДС
	|ПОМЕСТИТЬ СУкрупненнымиСтатьями
	|ИЗ
	|	ОбъединениеСгруп КАК ОбъединениеСгруп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.СтатьиКалькуляцииЗатрат КАК укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДССтатьиКалькуляцииЗатрат
	|		ПО ОбъединениеСгруп.СтатьяКалькуляции = укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДССтатьиКалькуляцииЗатрат.СтатьяКалькуляции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.УкрупненныйКодСведений,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.Код,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.Ссылка,
	|	ОбъединениеСгруп.СтатьяКалькуляции,
	|	ОбъединениеСгруп.ВидРаздельногоУчета,
	|	ОбъединениеСгруп.ВидОперации,
	|	ОбъединениеСгруп.ВсегоПоКалькуляциям,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.НДС
	|ИЗ
	|	Справочник.укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС КАК укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъединениеСгруп КАК ОбъединениеСгруп
	|		ПО (ИСТИНА)
	|ГДЕ
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.Ссылка = &СтатьяПрибыль
	|	И ОбъединениеСгруп.СтатьяКалькуляции = ""Прибыль""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.УкрупненныйКодСведений,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.Код,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.Ссылка,
	|	ОбъединениеСгруп.СтатьяКалькуляции,
	|	ОбъединениеСгруп.ВидРаздельногоУчета,
	|	ОбъединениеСгруп.ВидОперации,
	|	ОбъединениеСгруп.ВсегоПоКалькуляциям,
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.НДС
	|ИЗ
	|	Справочник.укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС КАК укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъединениеСгруп КАК ОбъединениеСгруп
	|		ПО (ИСТИНА)
	|ГДЕ
	|	укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС.Ссылка = &СтатьяНДС
	|	И ОбъединениеСгруп.СтатьяКалькуляции = ""НДС""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.ПонижающийКоэффициент, 1)) КАК ПонижающийКоэффициент,
	|	ОбъединениеСгруп.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ОбъединениеСгруп.ВидОперации КАК ВидОперации
	|ПОМЕСТИТЬ ТаблицаСКоэффициентами
	|ИЗ
	|	ОбъединениеСгруп КАК ОбъединениеСгруп
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляции.СрезПоследних(, ) КАК укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних
	|		ПО ОбъединениеСгруп.ВидОперации = укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.ВидОперации
	|			И ОбъединениеСгруп.СтатьяКалькуляции = укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.СтатьяКалькуляции
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъединениеСгруп.СтатьяКалькуляции,
	|	ОбъединениеСгруп.ВидОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъединениеСгруп.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ОбъединениеСгруп.СуммаФиксированная + ОбъединениеСгруп.СуммаОриентировочная * ЕСТЬNULL(ТаблицаСКоэффициентами.ПонижающийКоэффициент, 1) КАК Лимит
	|ПОМЕСТИТЬ КалькуляцииСКоэффициентами
	|ИЗ
	|	ОбъединениеСгруп КАК ОбъединениеСгруп
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСКоэффициентами КАК ТаблицаСКоэффициентами
	|		ПО ОбъединениеСгруп.ВидОперации = ТаблицаСКоэффициентами.ВидОперации
	|			И ОбъединениеСгруп.СтатьяКалькуляции = ТаблицаСКоэффициентами.СтатьяКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУкрупненнымиСтатьями.УкрупненныйКод КАК УкрупненныйКод,
	|	СУкрупненнымиСтатьями.ДетализированныйКод КАК ДетализированныйКод,
	|	СУкрупненнымиСтатьями.ДетализированнаяСтатьяУФК КАК ДетализированнаяСтатьяУФК,
	|	СУкрупненнымиСтатьями.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	СУкрупненнымиСтатьями.ВсегоПоКалькуляциям * ЕСТЬNULL(укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.УдельныйВес, 1) КАК ИтогоПоКалькуляции,
	|	ВЫБОР
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|			ТОГДА 0.18
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|			ТОГДА 0.2
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|			ТОГДА 0.1
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 0
	|	КОНЕЦ * СУкрупненнымиСтатьями.ВсегоПоКалькуляциям * ЕСТЬNULL(укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.УдельныйВес, 1) КАК СуммаНДС,
	|	СУкрупненнымиСтатьями.ВсегоПоКалькуляциям * ЕСТЬNULL(укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.УдельныйВес, 1) + ВЫБОР
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|			ТОГДА 0.18
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|			ТОГДА 0.2
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10_110)
	|			ТОГДА 0.1
	|		КОГДА СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ИЛИ СУкрупненнымиСтатьями.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 0
	|	КОНЕЦ * СУкрупненнымиСтатьями.ВсегоПоКалькуляциям * ЕСТЬNULL(укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.УдельныйВес, 1) КАК ПланРасходаВсего,
	|	ЕСТЬNULL(укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.УдельныйВес, 1) КАК УдельныйВес
	|ПОМЕСТИТЬ ТаблицаСУдельнымВесом
	|ИЗ
	|	СУкрупненнымиСтатьями КАК СУкрупненнымиСтатьями
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляции.СрезПоследних КАК укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних
	|		ПО СУкрупненнымиСтатьями.СтатьяКалькуляции = укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.СтатьяКалькуляции
	|			И СУкрупненнымиСтатьями.ДетализированнаяСтатьяУФК = укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.СтатьяУФК
	|			И СУкрупненнымиСтатьями.ВидРаздельногоУчета = укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.ВидРаздельногоУчета
	|			И СУкрупненнымиСтатьями.ВидОперации = укбп_УдельныйВесДетализированногоКодаУФКВСтатьеКалькуляцииСрезПоследних.ВидОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСУдельнымВесом.УкрупненныйКод КАК УкрупненныйКод,
	|	ТаблицаСУдельнымВесом.ДетализированныйКод КАК ДетализированныйКод,
	|	ТаблицаСУдельнымВесом.ДетализированнаяСтатьяУФК КАК ДетализированнаяСтатьяУФК,
	|	ТаблицаСУдельнымВесом.ИтогоПоКалькуляции КАК ИтогоПоКалькуляции,
	|	ТаблицаСУдельнымВесом.СуммаНДС КАК СуммаНДС,
	|	ТаблицаСУдельнымВесом.ПланРасходаВсего КАК ПланРасходаВсего,
	|	ТаблицаСУдельнымВесом.УдельныйВес * ЕСТЬNULL(КалькуляцииСКоэффициентами.Лимит, 0) + ТаблицаСУдельнымВесом.СуммаНДС КАК ЛимитРасходованияСведения
	|ПОМЕСТИТЬ ТаблицаДетальныеЗаписиПоСтатьямКалькуляции
	|ИЗ
	|	ТаблицаСУдельнымВесом КАК ТаблицаСУдельнымВесом
	|		ЛЕВОЕ СОЕДИНЕНИЕ КалькуляцииСКоэффициентами КАК КалькуляцииСКоэффициентами
	|		ПО ТаблицаСУдельнымВесом.СтатьяКалькуляции = КалькуляцииСКоэффициентами.СтатьяКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.УкрупненныйКод КАК УкрупненныйКод,
	|	ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.ДетализированныйКод КАК ДетализированныйКод,
	|	ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.ДетализированнаяСтатьяУФК КАК ДетализированнаяСтатьяУФК,
	|	СУММА(ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.ПланРасходаВсего) КАК ПланРасходаВсего,
	|	СУММА(ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.ЛимитРасходованияСведения) КАК ЛимитРасходованияСведения
	|ПОМЕСТИТЬ ТолькоСтатьиУФК
	|ИЗ
	|	ТаблицаДетальныеЗаписиПоСтатьямКалькуляции КАК ТаблицаДетальныеЗаписиПоСтатьямКалькуляции
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.ДетализированныйКод,
	|	ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.УкрупненныйКод,
	|	ТаблицаДетальныеЗаписиПоСтатьямКалькуляции.ДетализированнаяСтатьяУФК
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммаНДСПоСтатьеНДС.ДетализированнаяСтатьяУФК КАК ДетализированнаяСтатьяУФК,
	|	СУММА(СуммаНДСПоСтатьеНДС.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ СтатьяТолькоНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(&СтатьяНДС КАК Справочник.укбп_СтатьиУльяновскогоКазначействаПоРасходованиюДС) КАК ДетализированнаяСтатьяУФК,
	|		СУММА(ТолькоСтатьиУФК.СуммаНДС) КАК СуммаНДС
	|	ИЗ
	|		ТолькоСтатьиУФК КАК ТолькоСтатьиУФК) КАК СуммаНДСПоСтатьеНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммаНДСПоСтатьеНДС.ДетализированнаяСтатьяУФК
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТолькоСтатьиУФК.ПланРасходаВсего - ТолькоСтатьиУФК.СуммаНДС) КАК ПланРасходаВсего
	|ПОМЕСТИТЬ ИтогиПоСтатьямУФК
	|ИЗ
	|	ТолькоСтатьиУФК КАК ТолькоСтатьиУФК
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТолькоСтатьиУФК.УкрупненныйКод КАК УкрупненныйКод,
	|	ТолькоСтатьиУФК.ДетализированныйКод КАК ДетализированныйКод,
	|	ТолькоСтатьиУФК.ДетализированнаяСтатьяУФК КАК ДетализированнаяСтатьяУФК,
	|	ТолькоСтатьиУФК.ПланРасходаВсего - ЕСТЬNULL(СтатьяТолькоНДС.СуммаНДС, 0) КАК ПланРасходаВсего,
	|	(ТолькоСтатьиУФК.ПланРасходаВсего - ЕСТЬNULL(СтатьяТолькоНДС.СуммаНДС, 0)) / ИтогиПоСтатьямУФК.ПланРасходаВсего КАК ДоляДляРасходованияВСведениях,
	|	ТолькоСтатьиУФК.ЛимитРасходованияСведения - ЕСТЬNULL(СтатьяТолькоНДС.СуммаНДС, 0) КАК ЛимитРасходованияСведения,
	|	(ТолькоСтатьиУФК.ЛимитРасходованияСведения - ЕСТЬNULL(СтатьяТолькоНДС.СуммаНДС, 0)) / ИтогиПоСтатьямУФК.ПланРасходаВсего КАК ДоляДляЛимита,
	|	ТаблицаСДенежнымиСредствами.СуммаКонечныйОстаток КАК ИтогоКИспользованиюОстатокДС
	|ПОМЕСТИТЬ ИтоговаяТаблица
	|ИЗ
	|	ТолькоСтатьиУФК КАК ТолькоСтатьиУФК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИтогиПоСтатьямУФК КАК ИтогиПоСтатьямУФК
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСДенежнымиСредствами КАК ТаблицаСДенежнымиСредствами
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьяТолькоНДС КАК СтатьяТолькоНДС
	|		ПО ТолькоСтатьиУФК.ДетализированнаяСтатьяУФК = СтатьяТолькоНДС.ДетализированнаяСтатьяУФК
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтоговаяТаблица.УкрупненныйКод КАК УкрупненныйКод,
	|	ИтоговаяТаблица.ДетализированныйКод КАК ДетализированныйКод,
	|	ИтоговаяТаблица.ДетализированнаяСтатьяУФК КАК СтатьяСведений,
	|	ВЫРАЗИТЬ(ИтоговаяТаблица.ИтогоКИспользованиюОстатокДС * ИтоговаяТаблица.ДоляДляРасходованияВСведениях КАК ЧИСЛО(15, 2)) КАК СуммаСведения,
	|	ВЫРАЗИТЬ(ИтоговаяТаблица.ИтогоКИспользованиюОстатокДС * ИтоговаяТаблица.ДоляДляЛимита КАК ЧИСЛО(15, 2)) КАК ЛимитРасходаПоСведениям
	|ИЗ
	|	ИтоговаяТаблица КАК ИтоговаяТаблица";
	
	Объект.СтатьиСведений.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура("РежимВыбора",Истина);

	ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаВыбораУправляемая",ПараметрыВыбора,ЭтаФорма);	

КонецПроцедуры
